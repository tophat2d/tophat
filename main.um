import (
	"canvas.um"
	"font.um"
	"image.um"
	"input.um"
	"rect.um"
	"signal.um"
	"std.um"
	"th.um"
	"window.um"
)

var (
	logo: image::Image
	fg: uint32
	bg: uint32
	lines: []str
	loadedFont: font::Font
)

fn onFrame(args: []any) {
	canvas::drawRect(bg, rect::mk(0, 0, window::wp.x, window::wp.y))

	p := th::Vf2{ 0, 1 }

	if !input::isPressed(input::Key.leftAlt) {
		for i in lines {
			s := 1
			if i == 1 {
				s = 2
			}

			canvas::drawText(
				lines[i],
				th::Vf2{
					(window::wp.x - canvas::textSize(lines[i], s).x) / 2,
					p.y},
				fg, s)
			p.y += 6 * s
		}
	} else {
		for i in lines {
			s := 1*(1/4.0)
			if i == 1 {
				s = 2*(1/4.0)
			}

			loadedFont.draw(
				lines[i],
				th::Vf2{
					(window::wp.x - loadedFont.measure(lines[i]).x * s) / 2,
					p.y},
				fg, s)
			p.y += 16 * s
		}
	}


	if input::isJustPressed(input::Key.enter) {
		if th::platform == th::Platform.windows {
			std::system("explorer \"https://tophat2d.dev/\"")
		} else if th::platform == th::Platform.linux {
			std::system("xdg-open https://tophat2d.dev/")
		}
	}

	const s = 1.5
	logo.draw(th::Transform{
		p: p.add(window::wp.sub(p).sub(logo.getDims().mulf(s)).divf(2)),
		s: th::vf2f(s)})
}

fn init*() {
	window::setup("Hello world", 600, 600)
	window::setViewport(th::Vf2{ 100, 100 })
	var err: std::Err

	loadedFont, err = font::load("etc/roboto.ttf", 16)
	std::exitif(err)

	logo, err = image::load("etc/logo/logo-normal-white.png")
	std::exitif(err)

	fg = 0xadadd5ff
	bg = 0x262626ff

	lines = []str {
		"WELCOME TO",
		"TOPHAT",
		"",
		"EXAMPLES ARE IN",
		"THE examples/",
		"DIRECTORY",
		"",
		"PRESS ENTER TO",
		"OPEN THE",
		"HOMEPAGE" }


	window::onFrame.register(onFrame)
}

